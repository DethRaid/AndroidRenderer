#version 460 core

#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_buffer_reference_uvec2 : enable

#include "shared/primitive_data.hpp"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

layout(buffer_reference, scalar, buffer_reference_align = 4) readonly buffer ScatterIndicesBuffer {
    uint scatter_indices[];
};
layout(buffer_reference, scalar, buffer_reference_align = 4) readonly buffer ScatterDataBuffer {
    uint scatter_data[];
};
layout(buffer_reference, scalar, buffer_reference_align = 4) buffer DestDataBuffer {
    uint destination_data[];
};

layout(push_constant) uniform Constants {
    ScatterIndicesBuffer scatter_indices_buffer;
    ScatterDataBuffer scatter_data_buffer;
    DestDataBuffer dest_data_buffer;
    uint num_data;
    uint data_size; // Size of the data to copy in uints
};

void main() {
    uint index = gl_GlobalInvocationID.x;

    if(index >= num_data) {
        return;
    }

    uint dest_index = scatter_indices_buffer.scatter_indices[index] * data_size;

    uint read_index = index * data_size;
    for(uint offset = 0; offset < data_size; offset++) {
        dest_data_buffer.destination_data[dest_index + offset] = scatter_data_buffer.scatter_data[read_index + offset];
    }
}
